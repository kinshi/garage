
     ;***********************
     ;* 22K BITS PER SECOND *
     ;* SAMPLER FOR THE     *
     ;* COVOX VOICE MASTER  *
     ;* BY STEVE GOLDSMITH  *
     ;* (C) 1988            *
     ;***********************
     ;
     PORTA1 = $DC00 ;CIA #1 DATA PORT REGISTER A
     DDRA1 = $DC02 ;CIA #1 DATA DIRECTION REGISTER PORT A
     VIC2CR = $D011 ; VIC 2 CONTROL REGISTER
     VOLUME = $D418 ;SID MASTER VOLUME CONTROL
     SAMPLE = $F7 ;WHERE SOUND IS RECORDED TO OR PLAYED FROM
     FBYTE = $F9 ;FREQ BYTE
     MAXREC = $FA ;MAX # OF BLOCKS YOU CAN RECORD
     MS5 = $FB ;5 MICRO SEC DELAY       NUMBER
     BITCNT = $FC ;BIT COUNT
     ;
     * = $C000 ;START OF PROGRAM
     ;
C000 A9 00                 LDA #$00
C002 85 F7                 STA SAMPLE
C004 A9 08                 LDA #$08
C006 85 F8                 STA SAMPLE+1
C008 A9 80                 LDA #$80
C00A 85 FA                 STA MAXREC
C00C 20 22 C0              JSR RECORD
C00F 60                    RTS
C010 A9 00                 LDA #$00
C012 8D 9A C0              STA LOOP9+1
C015 A9 08                 LDA #$08
C017 8D 9B C0              STA LOOP9+2
C01A A9 80                 LDA #$80
C01C 85 FA                 STA MAXREC
C01E 20 86 C0              JSR PLAY
C021 60                    RTS
     ;
     ;RECORD SOUND FROM DIGITIZER
     ;
C022 78        RECORD      SEI ;DISABLE IRQ INTERRUPT
C023 A9 88                 LDA %10001000
C025 8D 02 DC              STA DDRA1 ;SET BITS 3&7 TO OUTPUTS AND THE REST TO INPUTS
C028 A9 00                 LDA #$00
C02A AA                    TAX ;INITILIZE FREQ BYTE
C02B A8                    TAY ;INITILIZE BYTE COUNTER
C02C 8D 00 DC              STA PORTA1 ;INITILIZE DIGITIZER
C02F AD 00 DC  LOOP1       LDA PORTA1
C032 29 10                 AND %00010000 ;WAIT FOR AMPLE VOLUME TO START RECORDING
C034 D0 F9                 BNE LOOP1
C036 AD 11 D0              LDA VIC2CR
C039 29 EF                 AND %11101111 ;BLANK SCREEN
C03B 8D 11 D0              STA VIC2CR
C03E A9 08                 LDA #$08
C040 85 FC                 STA BITCNT
C042 EA        LOOP4       NOP ;-
C043 EA                    NOP ; -
C044 EA                    NOP ;  -
C045 EA                    NOP ;   - 14 MICRO SEC DELAY
C046 EA                    NOP ;  -
C047 EA                    NOP ; -
C048 EA                    NOP ;-
C049 86 F9     LOOP6       STX FBYTE ;0 FREQ BYTE
C04B A6 FC                 LDX BITCNT ;# OF BITS TO COUNT
C04D AD 00 DC  LOOP3       LDA PORTA1 ;READ DATA PORT
C050 29 01                 AND #$01 ;GET FREQ BIT
C052 05 F9                 ORA FBYTE ;ADD IT TO FREQ BYTE
C054 CA                    DEX ;COUNT DOWN # OF BITS LEFT
C055 F0 12                 BEQ LOOP2
C057 0A                    ASL A ;MAKE ROOM FOR NEXT FREQ BIT
C058 85 F9                 STA FBYTE
C05A EA                    NOP ;-
C05B EA                    NOP ; -
C05C EA                    NOP ;  -
C05D EA                    NOP ;   -
C05E EA                    NOP ;    -
C05F EA                    NOP ;     -
C060 EA                    NOP ;     - 24 MICRO SEC DELAY
C061 EA                    NOP ;    -
C062 EA                    NOP ;   -
C063 EA                    NOP ;  -
C064 EA                    NOP ; -
C065 EA                    NOP ;-
C066 4C 4D C0              JMP LOOP3
C069 91 F7     LOOP2       STA (SAMPLE),Y
C06B C8                    INY ;SEE IF AT END OF MEM PAGE
C06C D0 D4                 BNE LOOP4
C06E C6 FA                 DEC MAXREC ;# OF BLOCKS TO RECORD
C070 F0 05                 BEQ LOOP5
C072 E6 F8                 INC SAMPLE+1 ;SET UP FOR NEXT PAGE
C074 4C 49 C0              JMP LOOP6
C077 AD 11 D0  LOOP5       LDA VIC2CR
C07A 09 10                 ORA %00010000 ;TURN SCREEN ON
C07C 8D 11 D0              STA VIC2CR
C07F A9 FF                 LDA #$FF
C081 8D 02 DC              STA DDRA1 ;RESTORE TO KEYBOARD OUTPUT
C084 58                    CLI ;RESTORE IRQ INTERRUPT
C085 60                    RTS
     ;
     ;PLAY SOUND FROM MEMORY
     ;
C086 78        PLAY        SEI ;DISABLE IRQ INTERRUPT
C087 AD 11 D0              LDA VIC2CR
C08A 29 EF                 AND %11101111 ;BLANK SCREEN
C08C 8D 11 D0              STA VIC2CR
C08F A2 00                 LDX #$00 ;BYTE COUNTER
C091 C6 FB     LOOP7       DEC MS5 ;-
C093 C6 FB                 DEC MS5 ; - 15 MICRO SEC DELAY
C095 C6 FB                 DEC MS5 ;-
C097 A0 08     LOOP8       LDY #$08 ;BIT COUNTER
C099 3E FF FF  LOOP9       ROL $FFFF,X ;THIS HAS A SELF MODIFING ADDRESS
C09C 90 05                 BCC LOOP13
C09E A9 0F                 LDA #$0F
C0A0 4C A6 C0              JMP LOOP10
C0A3 EA        LOOP13      NOP ;MICRO SEC DELAY
C0A4 A9 00                 LDA #$00
C0A6 8D 18 D4  LOOP10      STA VOLUME
C0A9 88                    DEY
C0AA F0 0D                 BEQ LOOP11
C0AC EA                    NOP ;-
C0AD EA                    NOP ; -
C0AE EA                    NOP ;  -
C0AF EA                    NOP ;   -
C0B0 EA                    NOP ;    -
C0B1 EA                    NOP ;    - 20 MICRO SEC DELAY
C0B2 EA                    NOP ;   -
C0B3 EA                    NOP ;  -
C0B4 EA                    NOP ; -
C0B5 EA                    NOP ;-
C0B6 4C 99 C0              JMP LOOP9
C0B9 E8        LOOP11      INX
C0BA D0 D5                 BNE LOOP7
C0BC C6 FA                 DEC MAXREC
C0BE F0 06                 BEQ LOOP12
C0C0 EE 9B C0              INC LOOP9+2
C0C3 4C 97 C0              JMP LOOP8
C0C6 AD 11 D0  LOOP12      LDA VIC2CR
C0C9 09 10                 ORA %00010000 ;TURN SCREEN ON
C0CB 8D 11 D0              STA VIC2CR
C0CE 58                    CLI ;RESTORE IRQ INTERRUPT
C0CF 60                    RTS
     :E
$C000-C0D0
