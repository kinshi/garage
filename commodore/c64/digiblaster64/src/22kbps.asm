100 ;***********************
110 ;* 22K BITS PER SECOND *
120 ;* SAMPLER FOR THE     *
130 ;* COVOX VOICE MASTER  *
140 ;* BY STEVE GOLDSMITH  *
150 ;* (C) 1988            *
160 ;***********************
170 ;
1000 PORTA1 = $DC00 ;CIA #1 DATA PORT REGISTER A
1010 DDRA1 = $DC02 ;CIA #1 DATA DIRECTION REGISTER PORT A
1020 VIC2CR = $D011 ; VIC 2 CONTROL REGISTER
1030 VOLUME = $D418 ;SID MASTER VOLUME CONTROL
1040 SAMPLE = $F7 ;WHERE SOUND IS RECORDED TO OR PLAYED FROM
1050 FBYTE = $F9 ;FREQ BYTE
1060 MAXREC = $FA ;MAX # OF BLOCKS YOU CAN RECORD
1070 MS5 = $FB ;5 MICRO SEC DELAY       NUMBER
1080 BITCNT = $FC ;BIT COUNT
1085 ;
1090 * = $C000 ;START OF PROGRAM
1100 ;
1110 LDA #$00
1120 STA SAMPLE
1130 LDA #$08
1140 STA SAMPLE+1
1150 LDA #$80
1160 STA MAXREC
1170 JSR RECORD
1180 RTS
1190 LDA #$00
1200 STA LOOP9+1
1210 LDA #$08
1220 STA LOOP9+2
1230 LDA #$80
1240 STA MAXREC
1250 JSR PLAY
1260 RTS
1270 ;
1280 ;RECORD SOUND FROM DIGITIZER
1290 ;
1300 RECORD SEI ;DISABLE IRQ INTERRUPT
1310 LDA %10001000
1320 STA DDRA1 ;SET BITS 3&7 TO OUTPUTS AND THE REST TO INPUTS
1330 LDA #$00
1340 TAX ;INITILIZE FREQ BYTE
1350 TAY ;INITILIZE BYTE COUNTER
1360 STA PORTA1 ;INITILIZE DIGITIZER
1370 LOOP1 LDA PORTA1
1380 AND %00010000 ;WAIT FOR AMPLE VOLUME TO START RECORDING
1390 BNE LOOP1
1400 LDA VIC2CR
1410 AND %11101111 ;BLANK SCREEN
1420 STA VIC2CR
1430 LDA #$08
1440 STA BITCNT
1450 LOOP4 NOP ;-
1460 NOP ; -
1470 NOP ;  -
1480 NOP ;   - 14 MICRO SEC DELAY
1490 NOP ;  -
1500 NOP ; -
1510 NOP ;-
1520 LOOP6 STX FBYTE ;0 FREQ BYTE
1530 LDX BITCNT ;# OF BITS TO COUNT
1540 LOOP3 LDA PORTA1 ;READ DATA PORT
1550 AND #$01 ;GET FREQ BIT
1560 ORA FBYTE ;ADD IT TO FREQ BYTE
1570 DEX ;COUNT DOWN # OF BITS LEFT
1580 BEQ LOOP2
1590 ASL A ;MAKE ROOM FOR NEXT FREQ BIT
1600 STA FBYTE
1610 NOP ;-
1620 NOP ; -
1630 NOP ;  -
1640 NOP ;   -
1650 NOP ;    -
1660 NOP ;     -
1670 NOP ;     - 24 MICRO SEC DELAY
1680 NOP ;    -
1690 NOP ;   -
1700 NOP ;  -
1710 NOP ; -
1720 NOP ;-
1730 JMP LOOP3
1740 LOOP2 STA (SAMPLE),Y
1750 INY ;SEE IF AT END OF MEM PAGE
1760 BNE LOOP4
1770 DEC MAXREC ;# OF BLOCKS TO RECORD
1780 BEQ LOOP5
1790 INC SAMPLE+1 ;SET UP FOR NEXT PAGE
1800 JMP LOOP6
1810 LOOP5 LDA VIC2CR
1820 ORA %00010000 ;TURN SCREEN ON
1830 STA VIC2CR
1840 LDA #$FF
1850 STA DDRA1 ;RESTORE TO KEYBOARD OUTPUT
1860 CLI ;RESTORE IRQ INTERRUPT
1870 RTS
1880 ;
1890 ;PLAY SOUND FROM MEMORY
1900 ;
1910 PLAY SEI ;DISABLE IRQ INTERRUPT
1920 LDA VIC2CR
1930 AND %11101111 ;BLANK SCREEN
1940 STA VIC2CR
1950 LDX #$00 ;BYTE COUNTER
1960 LOOP7 DEC MS5 ;-
1970 DEC MS5 ; - 15 MICRO SEC DELAY
1980 DEC MS5 ;-
1990 LOOP8 LDY #$08 ;BIT COUNTER
2000 LOOP9 ROL $FFFF,X ;THIS HAS A SELF MODIFING ADDRESS
2010 BCC LOOP13
2020 LDA #$0F
2030 JMP LOOP10
2040 LOOP13 NOP ;MICRO SEC DELAY
2050 LDA #$00
2060 LOOP10 STA VOLUME
2070 DEY
2080 BEQ LOOP11
2090 NOP ;-
2100 NOP ; -
2110 NOP ;  -
2120 NOP ;   -
2130 NOP ;    -
2140 NOP ;    - 20 MICRO SEC DELAY
2150 NOP ;   -
2160 NOP ;  -
2170 NOP ; -
2180 NOP ;-
2190 JMP LOOP9
2200 LOOP11 INX
2210 BNE LOOP7
2220 DEC MAXREC
2230 BEQ LOOP12
2240 INC LOOP9+2
2250 JMP LOOP8
2260 LOOP12 LDA VIC2CR
2270 ORA %00010000 ;TURN SCREEN ON
2280 STA VIC2CR
2290 CLI ;RESTORE IRQ INTERRUPT
2300 RTS
2310 :E
