     ;==================================
     ;DIGI-BLASTER 64 1.0
     ;DIGITIZED SOUND PLAYER 02/28/92
     ;(C) 1992 STEVE GOLDSMITH
     ;ALL RIGHTS RESERVED
     ;==================================
     ;
     ;DESIGNED TO PLAY BACK SOUND
     ;SAMPLED WITH COVOX VOICE MASTER
     ;OR OTHER UNCOMPRESSED 1 BIT
     ;SQUARE WAVE FORMATS.
     ;
     ;REBEL 64 ASSEMBLER FORMAT
     ;
     ;CIA CHIP
     ;
     TIMEA2 = $DD04 ;CIA 2 TIMER A LO BYTE
     CRA2 = $DD0E ;CIA 2 CONTROL REGISTER A
     ;
     ;VIC CHIP
     ;
     VICCR = $D011 ; VIC 2 CONTROL REGISTER
     ;
     ;SID CHIP
     ;
     SIDVOL = $D418 ;SID MASTER VOLUME CONTROL
     ;
     ;0 PAGE MEMORY USED BY PLAY ROUTINE
     ;
     SAMSTA = $A3 ;SAMPLE STARTING ADDR
     SAMLEN = $A5 ;SAMPLE LENGTH
     SAMRAT = $A7 ;SAMPLE RATE = 255-CLOCK RATE
     SAMBYT = $A8 ;SAMPLE BYTE
     SAMAMP = $A9 ;VOLUME FOR HIGH BIT
     ;
     * = $C000 ;ORIGIN OF PROGRAM
     :FILE @0:DB64.OBJ,8 ;MAKE OBJ FILE
     ;
     ;PLAY BACK 1 BIT UNCOMPRESSED
     ;SQUARE WAVE SAMPLE THROUGH SID
     ;
     ;BEFORE CALLING PLAY SET:
     ;SAMSTA = START OF SAMPLE
     ;SAMLEN = LENGTH OF SAMPLE
     ;SAMRAT = 255-SAMRAT CLOCK CYCLES
     ;SAMAMP = VOLUME FOR 1 BITS
     ;
     PLAY = *
C000 78                    SEI ;DISABLE IRQ
C001 AD 11 D0              LDA VICCR ;READ VIC CONTROL REG
C004 29 EF                 AND %11101111 ;BLANK SCREEN MASK
C006 8D 11 D0              STA VICCR ;BLANK SCREEN
C009 A9 FF                 LDA #$FF ;TIMER LATCH VALUE
C00B 8D 04 DD              STA TIMEA2 ;SET TIMER LATCH LO
C00E A0 00                 LDY #$00 ;ZERO SAMSTA INDEX
C010 8C 05 DD              STY TIMEA2+1 ;SET TIMER LATCH HI
C013 A9 11                 LDA %00010001 ;CIA 2 CONTROL REGISTER BIT SETTINGS:
     ;
     ;0 1 START TIMER
     ;1 0 NO TIMER A OUTPUT ON PB6
     ;2 0 PULSE BIT 6 ONE CYCLE
     ;3 0 CONTINUOUS RUN MODE
     ;4 1 FORCE LATCH LOAD
     ;5 0 COUNT MACHINE CYCLES
     ;6 0 SERIAL INPUT MODE
     ;7 0 TOD CLOCK 60 HZ
     ;
C015 8D 0E DD              STA CRA2 ;START TIMER
C018 B1 A3     REP1        LDA (SAMSTA),Y ;REPEAT GET SAMPLE BYTE
C01A 85 A8                 STA SAMBYT ;MOVE TO 0 PAGE
C01C A2 08                 LDX #$08 ;COUNT 8 BITS
C01E AD 04 DD  REP2        LDA TIMEA2 ;REPEAT READ TIMER
C021 C5 A7                 CMP SAMRAT
C023 B0 F9                 BCS REP2 ;UNTIL TIMER <= SAMRAT
C025 06 A8                 ASL SAMBYT ;GET AMP BIT
C027 A5 A9                 LDA SAMAMP ;GET 1S BIT VOLUME
C029 B0 02                 BCS ELSE1 ;IF CARRY FLAG = 0 THEN
C02B A9 00                 LDA #$00 ;SET 0 BIT VOLUME
C02D 8D 18 D4  ELSE1       STA SIDVOL ;ELSE SET 1 BIT VOLUME
C030 CA                    DEX ;COUNT DOWN BITS LEFT TO PLAY
C031 D0 EB                 BNE REP2 ;UNTIL NO MORE BITS
C033 A5 A6                 LDA SAMLEN+1
C035 D0 04                 BNE ELSE2 ;IF LAST BLOCK THEN
C037 C4 A5                 CPY SAMLEN ;CHECK IF LAST BYTE
C039 F0 0A                 BEQ UNTIL1
C03B C8        ELSE2       INY ;ELSE SET UP TO READ NEXT BLOCK
C03C D0 DA                 BNE REP1
C03E E6 A4                 INC SAMSTA+1 ;SET UP TO READ NEXT BLOCK
C040 C6 A6                 DEC SAMLEN+1 ;COUNT DOWN BLOCKS TO PLAY
C042 4C 18 C0              JMP REP1
C045 AD 11 D0  UNTIL1      LDA VICCR ;UNTIL LAST BYTE PLAYED
C048 09 10                 ORA %00010000 ;UNBLANK SCREEN MASK
C04A 8D 11 D0              STA VICCR ;UNBLANK SCREEN
C04D 58                    CLI ;ENABLE IRQ
C04E 60                    RTS
     :END
$C000-C04F
