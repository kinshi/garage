100 ;***********************
110 ;* 26K BITS PER SECOND *
120 ;* SAMPLER FOR THE     *
130 ;* COVOX VOICE MASTER  *
140 ;* BY STEVE GOLDSMITH  *
150 ;* (C) 1988            *
160 ;***********************
170 ;
180 PORTA1 = $DC00 ;CIA #1 DATA PORT REGISTER A
190 DDRA1 = $DC02 ;CIA #1 DATA DIRECTION REGISTER PORT A
200 VIC2CR = $D011 ; VIC 2 CONTROL REGISTER
210 VOLUME = $D418 ;SID MASTER VOLUME CONTROL
220 SAMPLE = $F7 ;WHERE SOUND IS RECORDED TO OR PLAYED FROM
230 FBYTE = $F9 ;FREQ BYTE
240 MAXREC = $FA ;MAX # OF BLOCKS YOU CAN RECORD
250 MS5 = $FB ;5 MICRO SEC DELAY       NUMBER
260 BITCNT = $FC ;BIT COUNT
270 ;
280 * = $C000 ;START OF PROGRAM
290 ;
300 LDA #$00 ;RECORD SOUND
310 STA SAMPLE
320 LDA #$08
330 STA SAMPLE+1 ;SAMPLE STARTS AT $0800
340 LDA #$80 ;RECORD $80 BLOCKS
350 STA MAXREC
360 JSR RECORD
370 RTS
380 LDA #$00 ;PLAY SOUND THROUGH SID
390 STA LOOP9+1
400 LDA #$08
410 STA LOOP9+2 ;SAMPLE STARTS AT $0800
420 LDA #$80
430 STA MAXREC ;PLAY $80 BLOCKS
440 JSR PLAY
450 RTS
460 ;
470 ;RECORD SOUND FROM DIGITIZER
480 ;
490 RECORD SEI ;DISABLE IRQ INTERRUPT
500 LDA %10001000
510 STA DDRA1 ;SET BITS 3&7 TO OUTPUTS AND THE REST TO INPUTS
520 LDA #$00
530 TAX ;INITILIZE FREQ BYTE
540 TAY ;INITILIZE BYTE COUNTER
550 STA PORTA1 ;INITILIZE DIGITIZER
560 LOOP1 LDA PORTA1
570 AND %00010000 ;WAIT FOR AMPLE VOLUME TO START RECORDING
580 BNE LOOP1
590 LDA VIC2CR
600 AND %11101111 ;BLANK SCREEN
610 STA VIC2CR
620 LDA #$08
630 STA BITCNT
640 LOOP4 DEC MS5 ;- 7 MICRO SEC DELAY
650 NOP ;-
660 LOOP6 STX FBYTE ;0 FREQ BYTE
670 LDX BITCNT ;# OF BITS TO COUNT
680 LOOP3 LDA PORTA1 ;READ DATA PORT
690 AND #$01 ;GET FREQ BIT
700 ORA FBYTE ;ADD IT TO FREQ BYTE
710 DEX ;COUNT DOWN # OF BITS LEFT
720 BEQ LOOP2
730 ASL A ;MAKE ROOM FOR NEXT FREQ BIT
740 STA FBYTE
750 DEC MS5 ;-
760 DEC MS5 ; -
770 DEC MS5 ; - 17 MICRO SEC DELAY
780 NOP ;-
790 JMP LOOP3
800 LOOP2 STA (SAMPLE),Y
810 INY ;SEE IF AT END OF MEM PAGE
820 BNE LOOP4
830 INC SAMPLE+1 ;SET UP FOR NEXT PAGE
840 BPL LOOP6
850 LDA VIC2CR
860 ORA %00010000 ;TURN SCREEN ON
870 STA VIC2CR
880 LDA #$FF
890 STA DDRA1 ;RESTORE TO KEYBOARD OUTPUT
900 CLI ;RESTORE IRQ INTERRUPT
910 RTS
920 ;
930 ;PLAY SOUND FROM MEMORY
940 ;
950 PLAY SEI ;DISABLE IRQ INTERRUPT
960 LDA VIC2CR
970 AND %11101111 ;BLANK SCREEN
980 STA VIC2CR
990 LDX #$00 ;BYTE COUNTER
1000 LOOP7 NOP ;-
1010 NOP ; -
1020 NOP ; - 8 SEC TIME DELAY
1030 NOP ;-
1040 LOOP8 LDY #$08 ;BIT COUNTER
1050 LOOP9 ROL $FFFF,X ;THIS HAS A SELF MODIFING ADDRESS
1060 BCC LOOP13
1070 LDA #$0F
1080 JMP LOOP10
1090 LOOP13 NOP ;2 MICRO SEC DELAY
1100 LDA #$00
1110 LOOP10 STA VOLUME
1120 DEY
1130 BEQ LOOP11
1140 DEC MS5 ;-
1150 NOP ; -
1160 NOP ;  - 13 MICRO MICRO SEC DELAY
1170 NOP ; -
1180 NOP ;-
1190 JMP LOOP9
1200 LOOP11 INX
1210 BNE LOOP7
1220 INC LOOP9+2
1230 BPL LOOP8
1240 LDA VIC2CR
1250 ORA %00010000 ;TURN SCREEN ON
1260 STA VIC2CR
1270 CLI ;RESTORE IRQ INTERRUPT
1280 RTS
1290 :E
