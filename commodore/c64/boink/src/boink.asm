1000 ;=================================
1010 ;64 BOINK!
1020 ;(C) 1986 STEVE GOLDSMITH
1030 ;ALL RIGHTS RESERVED
1040 ;USE COMMODORE'S MACRO ASSEMBLER
1050 ;TO ASSEMBLE
1060 ;=================================
1070 ;
1080 IRQVEC = $0314 ;OS IRQ INTERRUPT VECTOR
1090 VIC2 = $D000 ;VIC2 CHIP
1100 SPRPTR = $07F8 ;SPRITE POINTER
1110 SID = $D400 ;SOUND CHIP
1120 PRINT = $FFD2 ;OUTPUT A CHARACTER
1130 * = $0900
1140 ;
1150 ;INITILIZE SPRITE, IRQ, AND PRINT MESSAGE
1160 ;
1170 LDA #$05
1180 STA TIMER ;INIT ROTATION TIMER
1190 LDA #$05
1200 STA STIMER ;INIT SOUND EFFECT TIMER
1210 LDA #$00
1220 STA XDIR ;INIT X DIR
1230 STA YDIR ;INIT Y DIR
1240 STA COLOR ;INIT REFLECTION COLOR
1250 STA SETOGL ;INIT SOUND EFFECT TOGGLE
1260 STA ROTDIR ;INIT ROTATION DIRECTION
1270 SEI
1280 LDA IRQVEC ;GET CURRENT IRQ VECTOR
1290 STA NORIRQ ;SET UP INDIRECT JUMP
1300 LDA IRQVEC+1
1310 STA NORIRQ+1
1320 LDA #<NEWIRQ ;CHANGE IRQ VECTOR
1330 STA IRQVEC
1340 LDA #>NEWIRQ
1350 STA IRQVEC+1
1360 CLI
1370 LDX #$00 ;PRINT MESSAGE
1380 LOOP17 LDA TEXT,X
1390 JSR PRINT
1400 INX
1410 CPX #$27
1420 BNE LOOP17
1430 LDA #$00 ;BLACK
1440 STA VIC2+$21 ;SCREEN COLOR
1450 LDA #$2C ;SPRITE #44
1460 STA SPRPTR ;TELL VIC2 WHERE SPRITE 0 IS
1470 LDA #$A0 ;X POSITION
1480 STA VIC2
1490 LDA #$81 ;Y POSITION
1500 STA VIC2+1
1510 LDA #$01
1520 STA VIC2+$1C ;SET MULTI-COLOR MODE
1530 LDA #$06 ;BLUE
1540 STA VIC2+$25 ;SET BIT PAIR 01
1550 LDA #$01 ;WHITE
1560 STA VIC2+$26 ;SET BIT PAIR 11
1570 LDA #$02 ;RED
1580 STA VIC2+$27 ;BIT PAIR 10
1590 LDA #$01
1600 STA VIC2+$17 ;EXPAND Y 2X
1610 STA VIC2+$1D ;EXPAND X 2X
1620 STA VIC2+$15 ;TURN SPRITE ON
1630 LDA #$0F
1640 STA SID+$18 ;TURN ON MASTER VOLUME
1650 RTS
1660 ;
1670 ;ROTATE AND MOVE SPRITE
1680 ;
1690 NEWIRQ LDA XDIR ;GET X DIRECTION 0=RIGHT ; 1=LEFT
1700 BNE LOOP1
1710 LDA VIC2+$10 ;GET MSB X POS
1720 BNE LOOP3 ;SET?
1730 LDA VIC2 ;GET LSB X POS
1740 CMP #$FF ;REACHED SPRITE SEEM?
1750 BNE LOOP2
1760 LDA #$01
1770 STA VIC2+$10 ;SET MSB X POS
1780 JMP LOOP2
1790 LOOP3 LDA VIC2
1800 CMP #$3C ;RIGHT EDGE OF SCREEN
1810 BNE LOOP2
1820 LDA #$01
1830 STA XDIR ;CHANGE DIRECTION
1840 STA ROTDIR ;CHANGE ROTATION DIR
1850 JSR SOUND ;SOUND EFFECT
1860 JMP LOOP4
1870 LOOP2 INC VIC2 ;MOVE RIGHT
1880 JMP LOOP4 ;MOVE Y DIR
1890 LOOP1 LDA VIC2+$10 ;GET MSB X POS
1900 BEQ LOOP6 ;SET?
1910 LDA VIC2 ;GET LSB X POS
1920 BNE LOOP5 ;REACHED SPRITE SEEM?
1930 LDA #$00
1940 STA VIC2+$10 ;SET MSB X POS
1950 JMP LOOP5
1960 LOOP6 LDA VIC2
1970 CMP #$08 ;LEFT EDGE OF SCREEN
1980 BNE LOOP5
1990 LDA #$00
2000 STA XDIR ;CHANGE DIRECTION
2010 STA ROTDIR ;CHANGE ROTATION DIR
2020 JSR SOUND ;SOUND EFFECT
2030 JMP LOOP4
2040 LOOP5 DEC VIC2 ;MOVE LEFT
2050 LOOP4 LDA YDIR ;GET Y DIRECTION 0=DOWN ; 1=UP
2060 BNE LOOP7
2070 LDA VIC2+1 ;GET Y POS
2080 CMP #$D6 ;BOTTOM OF SCREEN
2090 BNE LOOP8
2100 LDA #$01
2110 STA YDIR
2120 STA ROTDIR ;CHANGE ROTATION DIR
2130 JSR SOUND ;SOUND EFFECT
2140 JMP LOOP9
2150 LOOP8 INC VIC2+1 ;MOVE DOWN
2160 JMP LOOP9
2170 LOOP7 LDA VIC2+1 ;GET Y POS
2180 CMP #$2C ;TOP OF SCREEN
2190 BNE LOOP10
2200 LDA #$00
2210 STA YDIR
2220 STA ROTDIR ;CHANGE ROTATION DIR
2230 JSR SOUND ;SOUND EFFECT
2240 JMP LOOP9
2250 LOOP10 DEC VIC2+1 ;MOVE DOWN
2260 LOOP9 DEC TIMER
2270 BNE LOOP11 ;TIMER = 0?
2280 LDA #$05
2290 STA TIMER ;RESET TIMER
2300 LDA ROTDIR ;GET ROTATION DIRECTION
2310 BNE LOOP15 ;1= LEFT 0=RIGHT
2320 LDA SPRPTR
2330 CMP #$3F
2340 BNE LOOP12 ;HAS SPRITE MADE FULL ROTATION?
2350 LDA #$00
2360 STA COLOR ;RESET COLOR COUNTER
2370 LDA #$2C
2380 STA SPRPTR
2390 JMP LOOP11
2400 LOOP12 INC SPRPTR ;ROTATE 1 STEP RIGHT
2410 LDX COLOR
2420 INC COLOR
2430 LDA C1DATA,X
2440 STA VIC2+$26 ;COLOR REFLECTION
2450 JMP LOOP11
2460 LOOP15 LDA SPRPTR
2470 CMP #$2C
2480 BNE LOOP16 ;HAS SPRITE MADE FULL ROTATION?
2490 LDA #$13
2500 STA COLOR ;RESET COLOR COUNTER
2510 LDA #$3F
2520 STA SPRPTR
2530 JMP LOOP11
2540 LOOP16 DEC SPRPTR ;ROTATE 1 STEP LEFT
2550 LDX COLOR
2560 DEC COLOR
2570 LDA C1DATA,X
2580 STA VIC2+$26 ;COLOR REFLECTION
2590 LOOP11 LDA SETOGL
2600 BNE LOOP13
2610 JMP (NORIRQ) ;NORMAL IRQ PROCESSING
2620 LOOP13 DEC STIMER
2630 BNE LOOP14
2640 LDA #%10000000
2650 STA SID+$04 ;START RELEASE
2660 LDA #$00
2670 STA SETOGL ;RESET TOGGLE
2680 LOOP14 JMP (NORIRQ) ;NORMAL IRQ PROCESSING
2690 ;
2700 ;SOUND EFFECT
2710 ;
2720 SOUND LDA #$85
2730 STA SID+$05 ;SET ATTACK/DECAY
2740 LDA #$75
2750 STA SID+$06 ;SET SUSTAIN/RELEASE
2760 LDA #$88
2770 STA SID ;SET LO FREQ
2780 LDA #$03
2790 STA SID+1 ;SET HI FREQ
2800 LDA #%10000001
2810 STA SID+$04 ;SET RANDOM NOISE AND GATE BIT
2820 LDA #$05
2830 STA STIMER ;SET SUSTAIN TIMER
2840 STA SETOGL ;SET SOUND EFFECT TOGGLE
2850 RTS
2860 ;
2870 ;VARIABLES AND DATA
2880 ;
2890 TEXT .BYTE 13,05,'64 BOINK!'
2900 .BYTE 13,'BY STEVE GOLDSMITH'
2910 .BYTE 13,'10/06/86'
2920 C1DATA .BYTE 11,11,11,12,15,1,15,12,11,11,11,11,11,12,15,1,15,12,11,11
2930 TIMER .BYTE 0 ;ROTATION TIMER
2940 ROTDIR .BYTE 0 ;ROTATION DIRECTION
2950 XDIR .BYTE 0 ;SPRITE X DIRECTION
2960 YDIR .BYTE 0 ;SPRITE Y DIRECTION
2970 SETOGL .BYTE 0 ;SOUND EFFECT TOGGLE
2980 STIMER .BYTE 0 ;SUSTAIN TIMER
2990 COLOR .BYTE 0 ;CURRENT REFLECTION COLOR
3000 NORIRQ .BYTE 0,0 ;IRQ JUMP VECTOR
3010 .END
